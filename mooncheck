#!/usr/bin/env moon


unless arg[1]
	print "Usage: mooncheck <filename.moon> [<luacheck-arguments]"
	os.exit 1

class Cmd
	new: (cmd) =>
		@cmd = cmd
		@
	arg: (...) =>
		if @args
			@args ..= " " .. table.concat {...}, " "
		else
			@args = table.concat {...}, " "

		@
	run: =>
		cmd = io.popen "#{@cmd} #{@args} 2>&1"
		ret = cmd\read "*a"
		_, _, ec = cmd\close!
		ret, ec

opt, target, typeflag = "", ""

for i in *arg
	if i\match "^%-[a-zA-Z]+$"
		opt ..= " #{i}"
	elseif i\match "^%-%-type"
		typeflag = true
	else
		target = i

err_msg = (is_abort, ...) ->
	io.stderr\write "E: %s: %s: Failed to parse"\format(...) .. '\n'
	os.exit 1 if is_abort

cont, ec = Cmd("moonc")\arg("-p", target)\run!

if ec > 0
	err_msg true, target, cont\match("\n.-(%d+).*$")

fn = os.tmpname!
fh = io.open fn, "a+"
fh\write cont
fh\seek "set"

lc_tmp, ec = Cmd("luacheck")\arg(opt, "--formatter", "plain", "--no-color", fn)\run!

if ec > 0 and #lc_tmp < 1
	err_msg true, nil, lc_tmp

if typeflag
	cont, ec = Cmd("tlc")\arg("-w", "-s", "-o ''", fn)\run!

	if ec > 0 and #cont < 1
		err_msg true, nil, cont

	lc_tmp ..= cont

fh\close!
os.remove fn

mooncx_tmp = Cmd("moonc")\arg("-X", target)\run!
buf = {}

for l in mooncx_tmp\gmatch "(.-)\n"
	b, a = l\match("^%d+%s+(%d+):%[.*%]%s+>>%s(%d+)")

	if b_ = tonumber b
		buf[b_] = tonumber a

class Kks
	__newindex: (k) =>
		unless @[k] then rawset @, k, k
	add: (k) => @[k] = _
	pairs: => pairs @

lbuf = Kks!

for l in lc_tmp\gmatch "(.-)\n"
	ln, warn = l\match(":(%d+):.-%s+(.*)$")

	if ln_ = tonumber ln
		lbuf\add "W: #{target}:#{buf[ln_]}:#{warn}" if buf[ln_]

for _, v in lbuf\pairs!
	print v

